(ns speculative.update-syms
  (:require
   [clojure.data :as data]
   [clojure.pprint :refer [pprint]]
   [clojure.set :as set]
   [clojure.spec.test.alpha :as stest]
   [clojure.string :as str]
   [speculative.core]
   [speculative.impl.syms :as prev-syms]
   [speculative.set]
   [speculative.string]))

(defn cljsify [syms]
  (map
    (fn [sym]
      (let [ns (namespace sym)
            ns (str/replace ns #"^clojure\.core" "cljs.core")
            sym (symbol ns (name sym))]
        sym))
    syms))

(defn diff [s1 s2]
  (let [[s1 s2]  (data/diff s1 s2)]
    (if (and (empty? s1) (empty? s2))
      nil
      [s1 s2])))

(defn print-diff [sym curr prev]
  (when-let [[s1 s2] (diff curr prev)]
    (println (str/join " " [sym
                            (when (not-empty s1)
                              (str "added " s1))
                            (when (not-empty s2)
                              (str "removed" s2))]))))

(defn set-missing-ns [ns coll]
  (map #(if (namespace %)
          %
          (symbol ns (str %)))
       coll))

(let [{:keys [cljs clj base]} (clojure.edn/read-string (slurp "blacklist.edn"))]
  (def blacklist-clj (->> (into clj base)
                          (set-missing-ns "clojure.core")
                          (into (sorted-set))))
  (def blacklist-cljs (->> (into cljs base)
                           (set-missing-ns "cljs.core")
                           (into (sorted-set)))))

(def all-syms (stest/instrumentable-syms))
(def all-syms-clj (into (sorted-set) all-syms))
(def all-syms-cljs (->> (disj all-syms `re-matcher `re-groups) ;; remove symbols missing from cljs
                        cljsify
                        (into (sorted-set))))
(def instrumentable-syms-clj (set/difference all-syms-clj blacklist-clj))
(def instrumentable-syms-cljs (set/difference all-syms-cljs blacklist-cljs))

(defn -main [& args]
  (println "==== Changes")
  (print-diff "all-syms-clj" all-syms-clj prev-syms/all-syms-clj)
  (print-diff "all-syms-cljs" all-syms-cljs prev-syms/all-syms-cljs)
  (print-diff "blacklist-clj" blacklist-clj prev-syms/blacklist-clj)
  (print-diff "blacklist-cljs" blacklist-cljs prev-syms/blacklist-cljs)
  (print-diff "instrumentable-syms-clj" instrumentable-syms-clj prev-syms/instrumentable-syms-clj)
  (print-diff "instrumentable-syms-cljs" instrumentable-syms-cljs prev-syms/instrumentable-syms-cljs)
  (println "====")
  (println "Update src/speculative/impl/syms.cljc with changes? Y/n")
  (let [s (read-line)]
    (if (= "Y" s)
      (let [new-file
            (with-out-str
              (binding [clojure.pprint/*print-right-margin* 80]
                (println ";; GENERATED BY script/update-syms. DO NOT EDIT BY HAND.")
                (pprint '(ns speculative.impl.syms
                           {:no-doc true}
                           (:require [clojure.set]
                                     [clojure.string]))))
              (binding [clojure.pprint/*print-right-margin* 7]
                (pprint (list 'def 'all-syms-clj
                              (list 'quote all-syms-clj)))
                (pprint (list 'def 'all-syms-cljs
                              (list 'quote all-syms-cljs)))
                (pprint (list 'def 'blacklist-clj
                              (list 'quote blacklist-clj)))
                (pprint (list 'def 'blacklist-cljs
                              (list 'quote blacklist-cljs)))
                (pprint (list 'def 'instrumentable-syms-clj
                              (list 'quote instrumentable-syms-clj)))
                (pprint (list 'def 'instrumentable-syms-cljs
                              (list 'quote instrumentable-syms-cljs)))))]
        (do (spit "src/speculative/impl/syms.cljc" new-file)
            (println "Succesfully wrote file.")))
      (println "Not updated file."))))
